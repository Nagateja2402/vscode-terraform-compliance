# This policy ensures that Amazon Inspector EC2 scanning is enabled

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports
import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                     "inspector-ec2-scan-enabled",
	"message":                         "Amazon Inspector EC2 scanning should be enabled. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/inspector-controls.html#inspector-1 for more details.",
	"resource_aws_inspector2_enabler": "aws_inspector2_enabler",
}

# Variables
resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_inspector2_enabler).resources

# Check if EC2 scanning is enabled in all Inspector2 enabler resources
violations = collection.reject(resources, func(res) {
	resource_types = maps.get(res.values, "resource_types", [])
	return resource_types is not null and resource_types contains "EC2"
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules
main = rule {
	violations is empty
}
